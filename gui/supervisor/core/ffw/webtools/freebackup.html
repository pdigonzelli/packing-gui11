<script language="speedscript">
/*-----------------------------------------------------------------------*
  File........: freebackup.html
  Version.....: 1.1 - 1/11/2002
  Description : Backup one or more files in the same directory
  Input Param : none
  Output Param: <none>
  Author......: S.E. Southwell - BravePoint, Inc.
  Copyright...: FreeFramework Project - http://www.freeframework.org
  Created.....: 1/11/2002
  Notes.......:
 *-----------------------------------------------------------------------*/
{ ffw/lib/ffw_global.i }                    /*PATH SETTINGS AND OTHER GENERAL THINGS*/
{ ffw/lib/ffpplib.i }
{ ffw/lib/filetools.i }
{ ffw/lib/lognote.i }

IF NOT devCheck() THEN DO:
	logNote("Caution","{&file-name} was requested by " + remote_addr + " but could not be run with the agents in Production mode.").
	ShowErrorScreen("Unable to run {&file-name}.  Request was logged and will be reviewed.").  
	RETURN.
END.

DEFINE VAR v-directory 	AS CHAR NO-UNDO.
DEFINE VAR v-filelist	AS CHAR NO-UNDO.
DEFINE VAR v-errmsg     AS CHAR NO-UNDO.
DEFINE VAR v-filecount  AS INT NO-UNDO.
DEFINE VAR v-success    AS LOGICAL NO-UNDO. /*Whether it compiled or not*/
DEFINE VAR v-comment	AS CHAR NO-UNDO.    /*Other info from compiler*/
DEFINE VAR v-webpath	AS CHAR NO-UNDO.    /*web path used to call a given program*/
DEFINE VAR v-errcnt		AS INTEGER NO-UNDO.
DEFINE VAR v-thisfiletype AS CHAR NO-UNDO.
DEFINE VAR v-count      AS INTEGER NO-UNDO.

ASSIGN
 v-filelist = GET-VALUE("filename")
 v-directory = GET-VALUE("directory") + "{&SLASH}"
.

FUNCTION DoBackup RETURNS LOGICAL(
 INPUT mydir AS CHAR,
 INPUT myfile AS CHAR,
 OUTPUT mybakfile AS CHAR
):
	DEFINE VAR v-filetype 		AS CHAR NO-UNDO.
	DEFINE VAR v-thisfilespec 	AS CHAR NO-UNDO.
	DEFINE VAR v-noextfilename  AS CHAR NO-UNDO. /*filename without extension*/
	
	ASSIGN FILE-INFO:FILE-NAME = mydir + myfile.
	IF FILE-INFO:FULL-PATHNAME = ? OR INDEX(FILE-INFO:FILE-TYPE,"d") > 0
	 THEN DO:
	 	ASSIGN mybakfile = "File not found!".
	 	RETURN FALSE. 
	END. /*file didn't exist or was a directory*/
	
	ELSE DO: /*file is a regular file*/
		ASSIGN v-thisfilespec = FILE-INFO:FULL-PATHNAME.
        REPEAT:
            ASSIGN v-count = v-count + 1.
            ASSIGN mybakfile = v-thisfilespec + "." + STRING(YEAR(TODAY)) + STRING(MONTH(TODAY)) + STRING(DAY(TODAY)) + "-" + STRING(TIME / 3600,"99") + "-" + STRING(v-count) + ".bak".
            ASSIGN FILE-INFO:FILE-NAME = mybakfile.
            IF FILE-INFO:FULL-PATHNAME = ? THEN LEAVE. /*found a unique one*/
        END.
		OS-COPY VALUE(v-thisfilespec) VALUE(mybakfile).
        IF OS-ERROR = 0 THEN RETURN TRUE.
	    ELSE RETURN FALSE.
	END.
END FUNCTION.
</script>
<html>
<head>
	<title>FreeFrameWork Quick Backup</title>
	<link rel="STYLESHEET" type="TEXT/CSS" href="`ffstyles`freestyle.css">
</head>

<body background="`ffimages`lyellow.gif">
<table border="0" width="100%" cellspacing="0">
<tr>
	<td align="LEFT">
		<span class="sectionname">FreeFrameWork Quick Backup</span>
	</td>
	<td align= "RIGHT">
	<a href="freefiletools.html?directory=`url-encode(right-trim(v-directory,"{&SLASH}"),"query")`&filename=`url-encode(v-filelist,"query")`">[Back to File Tools]</a>
    </td>
</tr>
<tr>
    <td class="instructions" colspan="2">
        This program is intended only to let a developer make a quick backup copy of a 
        given file for testing and development purposes.  Real backups should be performed
        on a schedule, with removeable media.
    </td>
</tr>
</table>
<% IF v-filelist ne "" THEN DO v-filecount = 1 TO NUM-ENTRIES(v-filelist): 
	ASSIGN v-success = DoBackup(v-directory,ENTRY(v-filecount,v-filelist),OUTPUT v-comment).
%>
<% IF v-success THEN DO: %>
<br>
<img src="`ffimages`good.gif" height="25" width="25" align="absbottom">&nbsp;
<span class="success">`v-directory``ENTRY(v-filecount,v-filelist)`&nbsp; was copied to `v-comment`.</span>
<% END. /*successful backup*/ %>
<% ELSE DO: /*backup failed*/ %>
<br>
<img src="`ffimages`bad.gif" height="25" width="25" align="absbottom">&nbsp;
<span class="failure">`v-directory``ENTRY(v-filecount,v-filelist)` did not get backed up.</span>
<div class="failurenote">OS Error #`os-error`</div>
<% END. /*backup failed*/ %>
    <% /*This has no effect in verions 3.0 and 3.1a due to a known bug in WebSpeed*/
    PUT STREAM WEBSTREAM CONTROL NULL (0). /*flush the buffer*/
END. /*v-filecount*/ %>
<% ELSE DO: %>
<span class="failure">Nothing to back up.</span>
<% END. /* nothing to backup */ %>
<HR>
<a href="freefiletools.html?directory=`url-encode(right-trim(v-directory,"{&SLASH}"),"query")`&filename=`url-encode(v-filelist,"query")`">[Back to File Tools]</a>
</body>
</html>

