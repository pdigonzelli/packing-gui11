<script language="speedscript">
/*-----------------------------------------------------------------------*
  File........: freeview.html
  Version.....: 1.04 - 9/14/01
  Description : View text files
  Input Param : <none>
  Output Param: <none>
  Author......: S.E. Southwell - BravePoint, Inc. (770) 449-9696
  Copyright...: FreeFramework 2001  - http://www.freeframework.org
  Created.....: 5/4/2000
  Notes.......:
 *-----------------------------------------------------------------------*/
{ ffw/lib/ffw_global.i }                    /*PATH SETTINGS AND OTHER GENERAL THINGS*/
{ ffw/lib/ffpplib.i }
{ ffw/lib/lognote.i }
{ ffw/lib/filetools.i }

DEFINE VAR v-directory 	  AS CHAR NO-UNDO.
DEFINE VAR v-filename	  AS CHAR NO-UNDO.
DEFINE VAR v-errmsg       AS CHAR NO-UNDO.
DEFINE VAR v-textin       AS CHAR NO-UNDO.
DEFINE VAR v-linecount    AS INTEGER NO-UNDO.
DEFINE VAR v-fileopened   AS LOGICAL NO-UNDO.
DEFINE VAR v-typemode     AS CHAR NO-UNDO.
DEFINE VAR v-from         AS CHAR NO-UNDO.

DEFINE STREAM filestream.
DEFINE TEMP-TABLE tt-line 
 FIELD lineno AS INTEGER
 FIELD linetxt AS CHAR
 INDEX lineno IS PRIMARY UNIQUE lineno.
 
/*If in PRODUCTION mode, then bail! */

IF NOT devCheck() THEN DO:
	logNote("Caution","{&file-name} was requested by " + remote_addr + " but could not be run with the agents in Production mode.").
	ShowErrorScreen("Unable to run {&file-name}.  Request was logged and will be reviewed.").  
	RETURN.
END.

ASSIGN
 v-filename = GET-VALUE("filename")
 v-directory = RIGHT-TRIM(GET-VALUE("directory"),"{&SLASH}") + "{&SLASH}"
 v-typemode = "4gl"
 v-from = GET-VALUE("from")
.

ASSIGN FILE-INFO:FILE-NAME = v-directory + v-filename.
IF FILE-INFO:FULL-PATHNAME = ? OR INDEX(FILE-INFO:FILE-TYPE,"d") > 0
THEN DO:
    ASSIGN v-errmsg = "File not found!".
END. /*file didn't exist or was a directory*/
ELSE DO:
    DO ON ERROR UNDO, LEAVE: /*file is a regular file*/
        INPUT STREAM filestream FROM VALUE(FILE-INFO:FULL-PATHNAME).
        ASSIGN v-fileopened = TRUE.
        REPEAT: 
            IMPORT STREAM filestream UNFORMATTED v-textin.
            ASSIGN v-linecount = v-linecount + 1.
            CREATE tt-line.
            ASSIGN
             tt-line.lineno  = v-linecount
             tt-line.linetxt = v-textin
            .
        END.
        INPUT STREAM filestream CLOSE.
    END. /*DO BLOCK*/
    IF ERROR-STATUS:ERROR OR v-fileopened = FALSE THEN ASSIGN v-errmsg = "Cannot open file.  " + ERROR-STATUS:GET-MESSAGE(1).
    ELSE IF v-linecount = 0 THEN ASSIGN v-errmsg = "File was empty.".
END. /*FILE WAS FOUND*/
FUNCTION codeColor RETURNS CHAR(
 INPUT mytext AS CHAR
):
    DEFINE VAR v-cnt AS INTEGER NO-UNDO.
    ASSIGN mytext = REPLACE(mytext," ","&nbsp~;").
    ASSIGN mytext = REPLACE(mytext,"~t","&nbsp~;&nbsp~;&nbsp~;&nbsp~;").
    ASSIGN mytext = '<span class="' + v-typemode + '">' + mytext.
    IF INDEX(mytext,"&lt~;script") > 0 THEN ASSIGN v-typemode = "4gl".
    IF INDEX(mytext,"/script&gt~;") > 0 THEN ASSIGN v-typemode = "html".
    IF INDEX(mytext,"&lt~;%") > 0 THEN ASSIGN v-typemode = "4gl".
    IF INDEX(mytext,"%&gt~;") > 0 THEN ASSIGN v-typemode = "html".
    IF INDEX(mytext,"&lt~;!--wss") > 0 THEN ASSIGN v-typemode = "4gl".
    IF INDEX(mytext,"--&gt~;") > 0 THEN ASSIGN v-typemode = "html".
    IF INDEX(mytext,"`") > 0 THEN DO v-cnt = 1 TO NUM-ENTRIES(mytext,"`"):
        IF v-cnt MODULO 2 = 1 THEN ASSIGN ENTRY(v-cnt,mytext,"`") = ENTRY(v-cnt,mytext,"`") + '</span><span class="4gl">'.
        IF v-cnt MODULO 2 = 0 THEN ASSIGN ENTRY(v-cnt,mytext,"`") = ENTRY(v-cnt,mytext,"`") + '</span><span class="html">'.
    END.
    ASSIGN mytext = REPLACE(mytext,"&lt~;script",'</span><span class="4gl">&lt~;script').
    ASSIGN mytext = REPLACE(mytext,"/script&gt~;",'/script&gt~;</span><span class="html">').
    ASSIGN mytext = REPLACE(mytext,"&lt~;!--wss",'</span><span class="4gl">&lt~;!--wss').
    ASSIGN mytext = REPLACE(mytext,"--&gt~;",'/--&gt~;</span><span class="html">').
    ASSIGN mytext = REPLACE(mytext,"&lt~;%",'</span><span class="4gl">&lt~;%').
    ASSIGN mytext = REPLACE(mytext,"%&gt~;",'%&gt~;</span><span class="html">').
    ASSIGN mytext = mytext + "</span>".
    RETURN mytext.
END FUNCTION.
</script>
<html>
<head>
	<title>FreeFramework File Viewer</title>
	<link rel="STYLESHEET" type="TEXT/CSS" href="`ffstyles`freestyle.css">
</head>

<body background="`ffimages`lyellow.gif">
<table border="0" width="100%" cellspacing="0">
<tr>
	<td align="LEFT">
		<span class="sectionname">FreeFrameWork File Viewer</span>
	</td>
	<td align= "RIGHT">
    <% if v-from = "" then do: %>
	<a href="freefiletools.html?directory=`v-directory`" onClick="history.back(); return false;">[Back to File Tools]</a>
    <% end. else do:%>
	<a href="#" onClick="history.back(); return false;">[Back]</a>
    <% end. %>
    </td>
</tr>
</table>
<% if v-errmsg ne "" then do: %>
<span class="error">`v-errmsg`</span></br>
<% end. %>
<p class="sectionname">`FILE-INFO:FULL-PATHNAME`</p>
<hr>
<span class="fileview">
<% 
    FOR EACH tt-line:
%>
<nobr>`codecolor(HTML-ENCODE(tt-line.linetxt))`</nobr><br>
<% END. 
%>
</span>
</body>
</html>


